:Namespace Day06
    To←#.U.To
    Segs←#.U.Segs

      Silver←{
          ⎕IO←0
          lights←1000 1000⍴0
          +/∊lights⊣{
              words←¯6↑', 'Segs ⍵
              (x1 y1 x2 y2)←⍎¨words[1 2 4 5]
              (xr yr)←(x1 To x2)(y1 To y2)
              on←{lights[xr;yr]←1}  ⍝ set 1
              off←{lights[xr;yr]←0}  ⍝ set 0
              toggle←{lights[xr;yr]⍲←1}  ⍝ toggle 1←→0
              ⍬⊣(⍎⊃words)⍬
          }¨⍵  ⍝ Do operations literally for each line
      }

      Gold←{
          ⎕IO←0
          lights←1000 1000⍴0
          +/∊lights⊣{
              words←¯6↑', 'Segs ⍵
              (x1 y1 x2 y2)←⍎¨words[1 2 4 5]
              (xr yr)←(x1 To x2)(y1 To y2)
              on←{lights[xr;yr]+←1}  ⍝ increment
              off←{lights[xr;yr]⌈←0⊣lights[xr;yr]-←1}  ⍝ decrement, min 0
              toggle←{lights[xr;yr]+←2}  ⍝ add 2
              ⍬⊣(⍎⊃words)⍬
          }¨⍵  ⍝ Do operations literally for each line
      }

      SilverTest←{
          f←⍺⍺
          Assert←#.U.Assert
          Assert 9≡f⊂'turn on 0,0 through 2,2':
          Assert 1000000≡f⊂'turn on 0,0 through 999,999':
          Assert 249999≡f'turn on 0,0 through 499,499' 'turn off 499,499 through 500,500':
          Assert 700≡f'turn on 0,0 through 0,499' 'toggle 0,200 through 0,999':
          'All tests passed'
      }

      GoldTest←{
          f←⍺⍺
          Assert←#.U.Assert
          Assert 9≡f⊂'turn on 0,0 through 2,2':
          Assert 1000000≡f⊂'turn on 0,0 through 999,999':
          Assert 249999≡f'turn on 0,0 through 499,499' 'turn off 499,499 through 500,500':
          Assert 2100≡f'turn on 0,0 through 0,499' 'toggle 0,200 through 0,999':
          'All tests passed'
      }

    ∇ r←Data
      r←#.U.ReadLines'input/2015/6.txt'
    ∇

    ∇ TestAll
      '    Silver Test: ',Silver SilverTest 0
      '      Gold Test: ',Gold GoldTest 0
      'Silver Solution: ',⍕Silver Data
      '  Gold Solution: ',⍕Gold Data
    ∇
:EndNamespace
