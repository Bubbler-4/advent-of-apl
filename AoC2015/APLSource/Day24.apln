:Namespace Day24
    Cmat←#.U.Cmat

    Solve←{
        ⍝ Minimal product of minimal-length subsequence
        ⍝ whose sum equals that of whole array ÷⍺
        ⍝ Assumption: the elements are odd, and
        ⍝ dividing the rest evenly is very likely possible
        target ← ⍺÷⍨+/⍵
        length ← target⍸⍨ 1,+\(⊂∘⍒⌷⊢)⍵
        length +← 2|target+length  ⍝ Add 1 if target's parity ≠ index's
        mat ← ⍵[length Cmat ≢⍵]
        ⌊/×/ mat⌿⍨ target=+/mat
    }
    
    Silver←3∘Solve
    
    Gold←4∘Solve
    
    data←1 3 5 11 13 17 19 23 29 31 37 41 43 47 53 59 67 71 73 79 83 89 97 101 103 107 109 113
    
    ∇ TestAll;⎕PP
      ⎕PP←16
      'Silver Solution: ',⍕Silver data
      '  Gold Solution: ',⍕Gold data
    ∇
:EndNamespace
